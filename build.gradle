plugins {
  id 'net.researchgate.release' version '2.3.4'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'de.kotka.clojuresque'

repositories {
  mavenLocal()
  
  maven {
    url RELEASE_REPOSITORY_URL
  }
  
  mavenCentral()
  jcenter()
  
  maven { url "http://clojars.org/repo" }
}

ext.clojuresquePlugins = [
  common: "2.0.0"
]

dependencies {
  compile gradleApi()
  compile localGroovy()

  project.clojuresquePlugins.each { plugin, version ->
    compile "de.kotka.clojuresque:clojuresque-${plugin}:${version}"
  }
  compile "com.squarespace.de.kotka.gradle:gradle-utils:0.2.3.11"
  compile "org.clojure:clojure:1.7.0"
  compile "org.clojure:tools.namespace:0.2.11"

  testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
}

compileJava {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

compileGroovy {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

// NOTE: The 'RELEASE_REPOSITORY_URL', 'MAVEN_CENTRAL_USERNAME' 
//       and 'MAVEN_CENTRAL_PASSWORD' values are stored in the 
//       ~/.gradle/gradle.properties file.

publishing {

  repositories {
    maven {
      url RELEASE_REPOSITORY_URL
    }
  }
  
  publications {
    sqsp(MavenPublication) {
      groupId "com.squarespace.${group}"
      from components.java
    }
  }
}

release {
  git {
    requireBranch = 'master'
  }
  
  failOnUpdateNeeded = false
  failOnSnapshotDependencies = false
}

createReleaseTag.dependsOn publish
